plugins {
    id 'java'
    id 'application'
}

sourceSets {
    main.java.srcDirs = ['java/src']
    main.resources.srcDirs = ['java/resources']
}

dependencies {
    implementation project(':lib')
    implementation 'org.jetbrains:annotations:24.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

application {
    mainClass = 'year2023.App'

    applicationDefaultJvmArgs = [
            // Working jansi colors/progress bars through `gw run`
            '-Djansi.passthrough=true',

            // For day 23 ForkJoinPool
            '-Xss2m',
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += '--enable-preview'
}
